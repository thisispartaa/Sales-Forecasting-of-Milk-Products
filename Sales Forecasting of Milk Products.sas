PROC IMPORT OUT= Data1 DATAFILE= "E:\Users\hks210000\Desktop\pROJECT\Delivery_Stores.xlsx"
DBMS=xlsx REPLACE;
GETNAMES=YES;
DATAROW = 2;
RUN;

PROC IMPORT OUT= Data3 DATAFILE= "E:\Users\hks210000\Desktop\pROJECT\prod_milk.xls"
DBMS=xls REPLACE;
GETNAMES=YES;
DATAROW = 2;
RUN;

PROC IMPORT OUT= Data4 DATAFILE= "E:\Users\hks210000\Desktop\pROJECT\milk_groc_1114_1165.xlsx"
DBMS=xlsx REPLACE;
GETNAMES=YES;
DATAROW = 2;
RUN;
/*Created UPC key for joining*/
data Data4;
set Data4;
UPC =catx('-',put(SY,z2.),put(GE,z2.),put(VEND,z5.), put(ITEM, z5.));
*UPC = CATX("-", SY , GE , VEND , ITEM);
Run;

/* Joining milk_groc and prod_milk Tables */

PROC SQL;
CREATE TABLE JOIN1 AS
SELECT A.*, B.*
FROM DATA3 A, Data4 B
WHERE A.UPC=B.UPC
ORDER BY UPC;
QUIT;
/* Joining output from above join and delivery_store Table */

PROC SQL;
CREATE TABLE JOIN2 AS
SELECT A.*, B.*
FROM JOIN1 A, Data1 B
WHERE A.IRI_key=B.IRI_key
ORDER BY IRI_key;
QUIT;

/*
PROC Print DATA= join2(obs=15);
run;
*/

/*Getting top categories*/

PROC FREQ DATA = JOIN2 ORDER = Freq;
TABLE PRODUCT_TYPE PACKAGE PROCESS FAT_CONTENT FLAVOR_SCENT	ADDITIVES TYPE_OF_MILK L5;
RUN;


/*Dropping extra variables*/
DATA Data;
drop L1 L2 L3 L4 L9 Level SY GE VEND ITEM IRI_Key _STUBSPEC_1260RC OU Week Open Clsd Additives mskdname Market_name;
SET join2;
Run;

/*Setting dummy variables*/
/*PRODUCT_TYPE PACKAGE PROCESS FAT_CONTENT FLAVOR_SCENT	ADDITIVES TYPE_OF_MILK*/
data Model_data;
	set Data;
	/*F*/
	IF F = "NONE" THEN F_flag = 0;
	  ELSE F_flag = 1;
	/*L3*/
	LACTAID100=0 ;NESTLE_NESQUIK=0 ;SILK=0; DEANS=0;PRIVATE_LABEL=0;
	IF L5= 'PRIVATE LABEL' THEN PRIVATE_LABEL=1;
	ELSE IF L5= 'LACTAID 100' THEN LACTAID100=1;
	ELSE IF L5= 'NESTLE NESQUIK' THEN NESTLE_NESQUIK=1;
	ELSE IF L5= 'SILK' THEN SILK=1;
	ELSE IF L5='DEANS' THEN DEANS=1;
	/*Product_type*/
	PRODUCT_TYPE_MILK=0;PRODUCT_TYPE_MILK_SUBSTITUE=0; PRODUCT_TYPE_BUTTERMILK=0;
	IF PRODUCT_TYPE= 'MILK' THEN PRODUCT_TYPE_MILK=1;
	ELSE IF PRODUCT_TYPE= 'MILK SUBSTITUTE' THEN PRODUCT_TYPE_MILK_SUBSTITUE=1;
	ELSE IF PRODUCT_TYPE= 'BUTTERMILK' THEN PRODUCT_TYPE_BUTTERMILK=1;
	/*PACKAGE*/
	PACKAGE_CARTON=0; PACKAGE_PLASTIC_JUG=0; PACKAGE_PLASTIC_BOTTLE=0;
	IF PACKAGE= 'CARTON' THEN PACKAGE_CARTON=1;
	ELSE IF PACKAGE= 'PLASTIC JUG' THEN PACKAGE_PLASTIC_JUG=1;
	ELSE IF PACKAGE= 'PLASTIC BOTTLE' THEN PACKAGE_PLASTIC_BOTTLE=1;
	/*PROCESS*/
	PROCESS_PASTEURIZED_HOMOGENIZED=0;
	IF PROCESS= 'PASTEURIZED HMGNZD' THEN PROCESS_PASTEURIZED_HOMOGENIZED=1;
	/*FLAVOR_SCENT*/
	FLAVOR_SCENT_WHITE=0; FLAVOR_SCENT_CHOCOLATE=0; FLAVOR_SCENT_REGULAR=0;
	IF FLAVOR_SCENT= 'WHITE' THEN FLAVOR_SCENT_WHITE=1;
	ELSE IF FLAVOR_SCENT= 'CHOCOLATE' THEN FLAVOR_SCENT_CHOCOLATE=1;
	ELSE IF FLAVOR_SCENT= 'REGULAR' THEN FLAVOR_SCENT_REGULAR=1;
	/*TYPE_OF_MILK*/
	TYPE_Regular=0; TYPE_REDUCED_FAT=0; TYPE_LOWFAT=0;
	TYPE_SKIM=0;
	IF TYPE_OF_MILK= 'REGULAR' THEN TYPE_Regular=1;
	ELSE IF TYPE_OF_MILK= 'REDUCED FAT' THEN TYPE_REDUCED_FAT=1;
	ELSE IF TYPE_OF_MILK= 'LOWFAT' THEN TYPE_LOWFAT=1;
	ELSE IF TYPE_OF_MILK= 'SKIM' THEN TYPE_SKIM=1;
	/*D*/
	DISPLAY=1;
	IF D=0 THEN DISPLAY=0;
RUN;

PROC CONTENTS DATA=Model_data;RUN;

data Model_data;
set Model_Data;
Vol_EQ=Vol_EQ*16;
Volume_Sold_oz=Units*Vol_EQ;
Price_per_oZ = DOLLARS /(UNITS*VOL_EQ);
Feature_display=Display*F_Flag;
Run;

/*Checking Corrleation*/
Proc corr data=Model_data;
Var Volume_Sold_oz DISPLAY DOLLARS FLAVOR_SCENT_CHOCOLATE FLAVOR_SCENT_REGULAR FLAVOR_SCENT_WHITE F_flag NESTLE_NESQUIK LACTAID100 SILK PRIVATE_LABEL DEANS PACKAGE_CARTON PACKAGE_PLASTIC_BOTTLE PACKAGE_PLASTIC_JUG PROCESS_PASTEURIZED_HOMOGENIZED PRODUCT_TYPE_BUTTERMILK PRODUCT_TYPE_MILK PRODUCT_TYPE_MILK_SUBSTITUE TYPE_LOWFAT TYPE_REDUCED_FAT TYPE_Regular TYPE_SKIM UNITS VOL_EQ ;
Run;
/*Checking for multicollinearity*/
Proc reg data=Model_data;
Model DOLLARS= F_flag DISPLAY Feature_display PR NESTLE_NESQUIK LACTAID100 SILK PRIVATE_LABEL DEANS FLAVOR_SCENT_CHOCOLATE /*FLAVOR_SCENT_REGULAR*/ FLAVOR_SCENT_WHITE PACKAGE_CARTON PACKAGE_PLASTIC_JUG PACKAGE_PLASTIC_BOTTLE PROCESS_PASTEURIZED_HOMOGENIZED PRODUCT_TYPE_BUTTERMILK PRODUCT_TYPE_MILK /*PRODUCT_TYPE_MILK_SUBSTITUE*/ TYPE_LOWFAT TYPE_REDUCED_FAT TYPE_Regular TYPE_SKIM / VIF COLLIN;
Run;

/*Running a basic regression Model Flavor scent regular and milk substitute are correlated with some columns*/
Proc reg data=Model_data;
Model DOLLARS= F_flag DISPLAY Feature_display PR NESTLE_NESQUIK LACTAID100 SILK PRIVATE_LABEL DEANS FLAVOR_SCENT_CHOCOLATE /*FLAVOR_SCENT_REGULAR*/ FLAVOR_SCENT_WHITE PACKAGE_CARTON PACKAGE_PLASTIC_JUG PACKAGE_PLASTIC_BOTTLE PROCESS_PASTEURIZED_HOMOGENIZED PRODUCT_TYPE_BUTTERMILK PRODUCT_TYPE_MILK /*PRODUCT_TYPE_MILK_SUBSTITUE*/ TYPE_LOWFAT TYPE_REDUCED_FAT TYPE_Regular TYPE_SKIM;
Run;
/*Checking Heteroscadasticity*/
PROC MODEL;
PARMS B0 B1 B2 B3 B4 B5 B6 B7 B8 B9 B10 B11 B12 B13 B14 B15 B16 B17 B18 B19 B20;
DOLLARS= B0*F_flag + B1*DISPLAY + B2*Feature_display + B3*PR + B4*NESTLE_NESQUIK + B5*LACTAID100 + B6* SILK +B7*PRIVATE_LABEL + B8*DEANS +B9* FLAVOR_SCENT_CHOCOLATE /*FLAVOR_SCENT_REGULAR*/+B10* FLAVOR_SCENT_WHITE+B11* PACKAGE_CARTON +B12*PACKAGE_PLASTIC_JUG +B13* PACKAGE_PLASTIC_BOTTLE +B14*PROCESS_PASTEURIZED_HOMOGENIZED +B15*PRODUCT_TYPE_BUTTERMILK +B16*PRODUCT_TYPE_MILK /*PRODUCT_TYPE_MILK_SUBSTITUE*/ +B17*TYPE_LOWFAT +B18*TYPE_REDUCED_FAT +B19*TYPE_Regular +B20*TYPE_SKIM;
fit DOLLARS / white;
run;

/*Logit*/
data Logit_data;
	set Model_Data;
	L5_flag=0;
	IF L5= 'PRIVATE LABEL' THEN L5_flag=1;
	ELSE IF L5= 'LACTAID 100' THEN L5_flag=2;
	ELSE IF L5= 'NESTLE NESQUIK' THEN L5_flag=3;
	ELSE IF L5= 'SILK' THEN L5_flag=4;
	ELSE IF L5='DEANS' THEN L5_flag=5;
	Run;
/**/
	Proc freq data=logit_data; table L5_flag;Run;

ODS PDF FILE = 'E:\Users\hks210000\Desktop\Logistic_Model.pdf' STYLE= statistical;
Proc logistic data=Logit_data descending;
	model L5_flag = DOLLARS F_flag DISPLAY Feature_display PR FLAVOR_SCENT_CHOCOLATE /*FLAVOR_SCENT_REGULAR*/ FLAVOR_SCENT_WHITE PACKAGE_CARTON PACKAGE_PLASTIC_JUG PACKAGE_PLASTIC_BOTTLE PROCESS_PASTEURIZED_HOMOGENIZED PRODUCT_TYPE_BUTTERMILK PRODUCT_TYPE_MILK /*PRODUCT_TYPE_MILK_SUBSTITUE*/ TYPE_LOWFAT TYPE_REDUCED_FAT TYPE_Regular TYPE_SKIM;
run;
ODS PDF CLOSE;



